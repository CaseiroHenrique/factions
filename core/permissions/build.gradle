plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'conexao.code'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    implementation project(':common')
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}

processResources {
    inputs.property 'version', version
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') { expand version: version }
}

// Copia o plugin gerado para as pastas dos servidores lobby e factions
tasks.register('copyToFactions', Copy) {
    dependsOn tasks.named('shadowJar')
    from tasks.named('shadowJar')
    into new File('E:/hyze/factions/plugins')
}

tasks.register('copyToLobby', Copy) {
    dependsOn tasks.named('shadowJar')
    from tasks.named('shadowJar')
    into new File('E:/hyze/lobby/plugins')
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveBaseName.set('permissions')
    archiveClassifier.set('')
    mergeServiceFiles()
}

tasks.named('jar') {
    enabled = false
}

tasks.named('build') {
    dependsOn tasks.named('shadowJar')
    finalizedBy tasks.named('copyToFactions'), tasks.named('copyToLobby')
}
